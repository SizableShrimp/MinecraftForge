plugins {
    id 'org.ajoberstar.grgit' version '4.1.0'
    id 'com.github.ben-manes.versions'
    id 'org.javamodularity.moduleplugin' version '1.8.3' apply false
}
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'org.javamodularity.moduleplugin'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = 'forge'
        url = 'https://maven.minecraftforge.net/'
    }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

group = 'net.minecraftforge'
ext.asmVersion = 9.1

java.toolchain.languageVersion = JavaLanguageVersion.of(16)

version = grgit.describe(longDescr: true).split('-').with { "${it[0]}.${it[1]}" }

dependencies {
    compileOnly('org.jetbrains:annotations:21.0.1')
    implementation(project(':fmlloader'))
    implementation(project(':fmlcore'))
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}


ext {
    MANIFESTS = [
            '': [
                    'FMLModType': 'LANGPROVIDER',
                    'Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Git-Commit': grgit.head().abbreviatedId,
                    'Git-Branch': grgit.branch.current().getName(),
                    'Build-Number': "${System.getenv('BUILD_NUMBER')?:0}",
            ] as LinkedHashMap,
            'net/minecraftforge/fml/mclanguageprovider/': [
                    'Specification-Title':      'FMLLoader',
                    'Specification-Vendor':     'Forge Development LLC',
                    'Specification-Version':    '1',
                    'Implementation-Title':     'FML Loader',
                    'Implementation-Version':   '1.0',
                    'Implementation-Vendor':    'Forge'
            ] as LinkedHashMap
    ]
}

jar {
    doFirst {
        MANIFESTS.each { pkg, values ->
            if (pkg != '')
                manifest.attributes(values, pkg)
            else
                manifest.attributes(values)
        }
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            pom {
                name = 'FML Mod Loader'
                description = 'FML implementation of modlauncher'
                url = 'https://github.com/MinecraftForge/MinecraftForge'
                scm {
                    url = 'https://github.com/MinecraftForge/MinecraftForge'
                    connection = 'scm:git:git://github.com/MinecraftForge/MinecraftForge.git'
                    developerConnection = 'scm:git:git@github.com:MinecraftForge/MinecraftForge.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/MinecraftForge/MinecraftForge/issues'
                }

                developers {
                    developer {
                        id = 'cpw'
                        name = 'cpw'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = System.env.MAVEN_USER ?: 'not'
                password = System.env.MAVEN_PASSWORD ?: 'set'
            }
            url 'https://maven.minecraftforge.net/releases/'
        }
    }
}